plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    // gradle sdk
    implementation gradleApi()
    // 实现Transform的时候会用到，gradle插件版本：https://developer.android.com/studio/releases/gradle-plugin?hl=zh-cn#updating-gradle
    // 这里注意使用的gradle插件（AGP）版本不要超过你Android studio 最大支持的版本：https://developer.android.com/studio/releases?hl=zh-cn#android_gradle_plugin_and_android_studio_compatibility
    // gradle/wrapper 中设置你gradle版本
    implementation("com.android.tools.build:gradle:7.3.1") {
        exclude group: "org.ow2.asm"
    }
    implementation("org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10") {
        exclude group: "org.ow2.asm"
    }
    implementation("org.ow2.asm:asm:9.3")
    implementation("org.ow2.asm:asm-commons:9.3")
    implementation("org.ow2.asm:asm-util:9.3")
}

def group = 'com.wzc'
def versionCode = '1.0.0'
def pluginName = 'privacy_compliance'

//插件配置，这样就不需要创建META-INF/gradle-plugins文件目录了
gradlePlugin {
    plugins {
        //定义的信息可以和发布信息不相同
        MethodTracer {
            id = "${group}.${pluginName}"  //定义插件id
            version = versionCode
            implementationClass = 'com.wzc.privacy.compliance.plugin.PluginLaunch'  //定义插件实现类
        }
    }
}

publishing {
    publications {
        //PubName
        plugin(MavenPublication) {
            groupId = group
            artifactId = pluginName
            version = versionCode
            artifact("$buildDir/libs/privacy_compliance_plugin-${version}.jar")
        }
    }
}

task copyPluginFile {
    doLast {
        copy {
            from '../privacy_compliance_plugin/build/libs/'
            into '../plugin_libs'
            rename "privacy_compliance_plugin-${versionCode}.jar", "${pluginName}-${versionCode}.jar"
        }
    }
}

assemble.finalizedBy(copyPluginFile)
build.finalizedBy(copyPluginFile)
jar.finalizedBy(copyPluginFile)